
[DainaCommandParser : [Object]] (String, Object, DainaCommandReceiver, DainaCommandStrings, Mapping, DainaCommandParsingAction, ValidDainaCommandsToParse)
	[ValidDainaCommandsToParse] validDainaCommandsToParse
{
	~ newDainaCommandParser *() {
		\::newObject;
		.validDainaCommandsToParse = validDainaCommandsToParse
	} 	!{
			[ValidDainaCommandsToParse] validDainaCommandsToParse = \[ValidDainaCommandsToParse]:emptyListOfValidDainaCommandsToParse;
			\validDainaCommandsToParse:addValidDainaCommandForCommandStringAndParsingActionAndDescriptionOfCommand (\[DainaCommandStrings]:PARSE_DAINA_SOURCE_FILE) (\[DainaCommandParsingAction]:parseCommandToParseDainaSourceFile) (\[DainaCommandDescriptionStrings]:PARSE_DAINA_SOURCE_FILE);
			\validDainaCommandsToParse:addValidDainaCommandForCommandStringAndParsingActionAndDescriptionOfCommand (\[DainaCommandStrings]:ANALYSE_AND_SYNTHESISE_SYNTAX_TREE) (\[DainaCommandParsingAction]:parseCommandToAnalyseAndSynthesiseSyntaxTree) (\[DainaCommandDescriptionStrings]:ANALYSE_AND_SYNTHESISE_SYNTAX_TREE);
			\validDainaCommandsToParse:addValidDainaCommandForCommandStringAndParsingActionAndDescriptionOfCommand (\[DainaCommandStrings]:COMPILE_INTO_JAVA_WITHIN_DIRECTORY) (\[DainaCommandParsingAction]:parseCommandToCompileIntoJavaWithinDirectory) (\[DainaCommandDescriptionStrings]:COMPILE_INTO_JAVA_WITHIN_DIRECTORY);
			\validDainaCommandsToParse:addValidDainaCommandForCommandStringAndParsingActionAndDescriptionOfCommand (\[DainaCommandStrings]:COMPILE_INTO_C_WITHIN_DIRECTORY) (\[DainaCommandParsingAction]:parseCommandToCompileIntoCWithinDirectory) (\[DainaCommandDescriptionStrings]:COMPILE_INTO_C_WITHIN_DIRECTORY);
		}

	++ parseNextCommandAndSendToReceiverWithCommandStringSegmentProvider *([DainaCommandReceiver] dainaCommandReceiver, [->[String]] commandStringSegmentProvider) {
		[String] nextCommandString = \commandStringSegmentProvider;
		[Maybe<[DainaCommandParsingAction]>] maybeParsingActionForCommandString = \.mappingOfCommandStringToParsingAction:maybeRetriveMappedValueWithKey nextCommandString;
		[DainaCommandParsingAction] parsingActionForCommandString = \[ControlFlow<[DainaCommandParsingAction]>]:selectWithSelectorOrDefault maybeParsingActionForCommandString:isJust ([DainaCommandParsingAction]:parseInvalidCommand);
		\parsingActionForCommandString:performActionWithCommandStringSegmentProviderAndThenSendCommandToReceiver commandStringSegmentProvider dainaCommandReceiver;
	}	

	++ getDescriptionOfValidDainaCommands *->\.validDainaCommandsToParse:getDescriptionOfValidDainaCommands
}

[DainaCommandStrings] (String) -> (DainaCommandParser)
{
	:: PARSE_DAINA_SOURCE_FILE *->(\[String]:newString "parse dainasource")
	:: ANALYSE_AND_SYNTHESISE_SYNTAX_TREE *->(\[String]:newString "synthesise")
	:: COMPILE_INTO_JAVA_WITHIN_DIRECTORY *->(\[String]:newString "compileinto java")
	:: COMPILE_INTO_C_WITHIN_DIRECTORY *->(\[String]:newString "compileinto c")
} 

[DainaCommandDescriptionStrings] (String) -> (DainaCommandParser)
{
	:: PARSE_DAINA_SOURCE_FILE *->(\[String]:newString "next input will be parsed as a Daina source file and integrated into the current syntax tree and any errors will be given")
	:: ANALYSE_AND_SYNTHESISE_SYNTAX_TREE *->(\[String]:newString "current syntax tree will be synthesised and any errors will be given")
	:: COMPILE_INTO_JAVA_WITHIN_DIRECTORY *->(\[String]:newString "next input is where the Java source will be constructed from the current syntax tree and any errors will be given")
	:: COMPILE_INTO_C_WITHIN_DIRECTORY *->(\[String]:newString "next input is where the C source will be constructed from the current syntax tree and any errors will be given")
} 

[ValidDainaCommandsToParse : [Object]] (DainaCommandParsingAction, Object, Variable, String, Mapping) -> DainaCommandParser
	[Mapping<[String][DainaCommandParsingAction]>] mappingOfValidCommandStringToParsingAction
	[Variable<[String]>] descriptionOfAllValidCommands
{
	~ emptyListOfValidDainaCommandsToParse *{
		\::newObject;
		.descriptionOfAllValidCommands = \[String]:newStringWithLiteral "valid Daina commands are:";
		.mappingOfValidCommandStringToParsingAction = \[Mapping<[String][DainaCommandParsingAction]>]:newEmptyMapping;
	}

	++ getDescriptionOfValidDainaCommands *->.descriptionOfAllValidCommands:get

	++ maybeParsingActionForCommandStringIfValidDainaCommand*([String] commandStringInQuestion) -> [Maybe<[DainaCommandParsingAction]>] {
	} -> (\.mappingOfValidCommandStringToParsingAction:maybeRetriveMappedValueWithKey commandStringInQuestion)

	++ addValidDainaCommandForCommandStringAndParsingActionAndDescriptionOfCommand *([String] commandString, [DainaCommandParsingAction] parsingAction, [String] descriptionOfCommand) {
		\.mappingOfValidCommandStringToParsingAction:mapKeyToValue commandString parsingAction;
		\.descriptionOfAllValidCommands:set (\currentDescriptionOfAllValidCommands:stringByAppendingString (\newLineAndTab:stringByAppendingString descriptionOfCommand));
	}	!{
			[String] currentDescriptionOfAllValidCommands = \.descriptionOfAllValidCommands:get; 
			[String] newLineAndTab = \[String]:newStringWithLiteral "\n\t";
		}
}

[DainaCommandParsingAction] (Object) -> (DainaCommandParser)
	[[->[String]][DainaCommandReceiver]->] parsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
{
	~ newParsingActionUsingParsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver *([[->[String]][DainaCommandReceiver]->] parsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver) {
		\::newObject;
		.parsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver = parsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
	}

	++ performActionWithCommandStringSegmentProviderAndThenSendCommandToReceiver *([->[String]] commandStringSegmentProvider, [DainaCommandReceiver] dainaCommandReceiver) {
		\.parsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver commandStringSegmentProvider dainaCommandReceiver
	}

	~ parseCommandToParseDainaSourceFile *{
		\::newParsingActionUsingParsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
			*([->[String]] getNextCommandStringSegment, [DainaCommandReceiver] dainaCommandReceiver) {
				\dainaCommandReceiver:receiveCommandToParseDainaSourceFileWithPath (\getNextCommandStringSegment)
			}
	}

	~ parseCommandToAnalyseAndSynthesiseSyntaxTree *{
		\::newParsingActionUsingParsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
			*([->[String]] getNextCommandStringSegment, [DainaCommandReceiver] dainaCommandReceiver) {
				\dainaCommandReceiver:receiveCommandToAnalyseAndSynthesiseSyntaxTree
			}
	}

	~ parseCommandToCompileIntoJavaWithinDirectory *{
		\::newParsingActionUsingParsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
			*([->[String]] getNextCommandStringSegment, [DainaCommandReceiver] dainaCommandReceiver) {
				\dainaCommandReceiver:receiveCommandToCompileIntoJavaWithinDirectory (\getNextCommandStringSegment)
			}
	}

	~ parseCommandToCompileIntoCWithinDirectory *{
		\::newParsingActionUsingParsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
			*([->[String]] getNextCommandStringSegment, [DainaCommandReceiver] dainaCommandReceiver) {
				\dainaCommandReceiver:receiveCommandToCompileIntoCWithinDirectory (\getNextCommandStringSegment)
			}
	}

	~ parseInvalidCommand *{
		\::newParsingActionUsingParsingActionToParseCommandWithCommandStringSegmentProviderAndThenSendCommandToReceiver
			*([->[String]] getNextCommandStringSegment, [DainaCommandReceiver] dainaCommandReceiver) {
				\dainaCommandReceiver:receiveInvalidCommand
			}
	}

}





