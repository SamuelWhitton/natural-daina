

[DLangSynthesizer] (
			Object, 
			Parser, 
			List,
			Loops,
			Token,
			Boolean,
			Maybe,
			String,
			Error, M, O,
			DainaDependancyStructure,
			DainaExpression,
			Variable,
			DainaClassIdentifier,
			DainaGenericDeclarationList,
			DainaType,
			DainaObjectDeclaration,
			DainaClassMethod,
			DainaCompilerInjection,
			OutputStream,
			DainaClass,
			Integer,
			DainaGeneralIdentifier,
			Mapping,
			Debug
		)
	[OutputStream] output
	[Integer] tab
	[Boolean] blockOutput
	[Mapping<[String][DainaClass]>] mappedClasses
	[Mapping<[String][Mapping<[String][DainaClassMethod<[DainaExpression]>]>]>] mappedClassMethods
	[DainaClassIdentifier] selfClassName
	[List<[DainaObjectDeclaration]>] selfInstanceObjects
	[Integer] temporaryNumber
	[DainaType] synthesizedExpressionType
	[Mapping<[String][DainaType]>] localObjectScope
{
	~ to *([OutputStream] output) {
		.tab = \[Integer]:zero;
		.output = output;
		.mappedClasses = \[Mapping<[String][DainaClass]>]:empty;
		.mappedClassMethods = \[Mapping<[String][Mapping<[String][DainaClassMethod<[DainaExpression]>]>]>]:empty;
		.temporaryNumber = \[Integer]:zero;
		.selfClassName = \[DainaClassIdentifier]:base;
		.synthesizedExpressionType = \[DainaType]:base;
		.selfInstanceObjects = \[List<[DainaObjectDeclaration]>]:empty;
		.localObjectScope = \[Mapping<[String][DainaType]>]:empty;
		.blockOutput = \[Boolean]:false;
	}

	- outputNextTemporaryVariableName *{
		.temporaryNumber = \.temporaryNumber:adding (\[Integer]:one);
		\:output (\[String]:as "temp");
		\:output (\[String]:stringRepresentationOfInteger .temporaryNumber);
	}

	- output *([String] string) {
		\[O]:ifElse .blockOutput *{} *{
			\.output:writeString string;
		};
	}

	- outputTabbed *([String] string) {
		\[O]:ifElse .blockOutput *{} *{
			\[Loops]:repeatNTimes .tab *{
				\.output:writeString (\[String]:as "   ");
			};
			\.output:writeString string;
		};
	}

	- incrementTab *{
		.tab = \.tab:integerByAdding (\[Integer]:one);
	}

	- decrementTab *{
		.tab = \.tab:integerBySubtracting (\[Integer]:one);
	}

	- iterateUnoverridenInstanceMethods *([DainaClass] class, [[Integer][DainaClassMethod<[DainaExpression]>]->] get) {
		\class:isEntryPointOrRegularClass *([DainaDependancyStructure] dependancyStructure, [DainaExpression] entryPointMethod) {
		} *([DainaClassIdentifier] className, [List<[DainaType]>] inheritedParentTypes, [List<[DainaObjectDeclaration]>] instanceObjects, [List<[DainaClassMethod<[DainaExpression]>]>] classMethods, [List<[DainaCompilerInjection]>] compilerInjections) {
			[Variable<[Integer]>] varParentNumber = \[Variable<[Integer]>]:as (\[Integer]:one);
			\inheritedParentTypes:iterate *([DainaType] parentType) {
				[Integer] parentNumber = \varParentNumber:get;
				\varParentNumber:set (\parentNumber:adding (\[Integer]:one));
				\parentType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] parentTypeIdentifier, [List<[DainaType]>] genericInstantiation) {
					[String] parentID = (\parentTypeIdentifier:content);
					\.mappedClasses:retriveMappedValueWithKey parentID *([DainaClass] parentClass) {
						\:iterateUnoverridenInstanceMethods parentClass *([Integer] i, [DainaClassMethod<[DainaExpression]>]c) {
							\getIfNotOverriden parentNumber c;
						};
						\parentClass:isEntryPointOrRegularClass *([DainaDependancyStructure] dependancyStructure, [DainaExpression] entryPointMethod) {
						} *([DainaClassIdentifier] a1, [List<[DainaType]>] a2, [List<[DainaObjectDeclaration]>] a3, [List<[DainaClassMethod<[DainaExpression]>]>] parentClassMethods, [List<[DainaCompilerInjection]>] a5) {
							\parentClassMethods:iterate *([DainaClassMethod<[DainaExpression]>] parentClassMethod) {
								\getIfNotOverriden parentNumber parentClassMethod;
							};
						};
					}; 	
				};
			};

		} !{
				[[Integer][DainaClassMethod<[DainaExpression]>]->] getIfNotOverriden = *([Integer] parentNumber, [DainaClassMethod<[DainaExpression]>] parentMethod) {
					[DainaGeneralIdentifier] parentMethodIdentifier = \parentMethod:identifier;
					\.mappedClassMethods:retriveMappedValueWithKey (\className:content) *([Mapping<[String][DainaClassMethod<[DainaExpression]>]>] ownClassMethods) {
						[Maybe<[DainaClassMethod<[DainaExpression]>]>] ifOverriden = \ownClassMethods:maybeRetriveMappedValueWithKey (\parentMethodIdentifier:content);
						\ifOverriden:ifNothing *{
							\get parentNumber parentMethod;
						};
					};
				};
		   };
	}	

	++ synthesizeClasses *([List<[DainaClass]>] classes) {
		\classes:iterate *([DainaClass] class) {
			\class:isEntryPointOrRegularClass *([DainaDependancyStructure] dependancyStructure, [DainaExpression] entryPointMethod) {
			} *([DainaClassIdentifier] className, [List<[DainaType]>] inheritedParentTypes, [List<[DainaObjectDeclaration]>] instanceObjects, [List<[DainaClassMethod<[DainaExpression]>]>] classMethods, [List<[DainaCompilerInjection]>] compilerInjections) {
				\.mappedClasses:mapKeyToValue (\className:content) class;
				[Mapping<[String][DainaClassMethod<[DainaExpression]>]>] classMethodsMapping = \[Mapping<[String][DainaClassMethod<[DainaExpression]>]>]:empty;
				\classMethods:iterate *([DainaClassMethod<[DainaExpression]>] classMethod) {
					[DainaGeneralIdentifier] classMethodIdentifier = \classMethod:identifier;
					\classMethodsMapping:mapKeyToValue (\classMethodIdentifier:content) classMethod;
				};
				\.mappedClassMethods:mapKeyToValue (\className:content) classMethodsMapping;
			};	
		};
		\:output (\[String]:as "/* *** HEADER *** */\n");
		\:output (\[String]:as "interface OBJECT {}\n");
		\:output (\[String]:as "class LITERAL_STRING : OBJECT { string it; this(string s) {this.it = s;} }\n");
		\:output (\[String]:as "interface LAMBDA {\n   OBJECT method(OBJECT caller, OBJECT[] parameters);\n}\n\n");
		\classes:iterate :synthesizeClass;
	}

	- synthesizeClass *([DainaClass] class) {
		\class:isEntryPointOrRegularClass *([DainaDependancyStructure] dependancyStructure, [DainaExpression] entryPointMethod) {
			\:output (\[String]:as "/* *** ENTRY POINT CLASS *** */\n");
			\:output (\[String]:as "void main() { ENTRY_POINT_CLASS.entryPointMethod(null, []); }\n"); 
			\:output (\[String]:as "class ENTRY_POINT_CLASS {\n");
			\:output (\[String]:as "   static OBJECT entryPointMethod(OBJECT caller, OBJECT[] parameters) {\n"); \:incrementTab; \:incrementTab;
			\:synthesizeMethodBodyContent entryPointMethod; 
			\:decrementTab; \:decrementTab; \:output (\[String]:as "   }\n}\n\n");
		} *([DainaClassIdentifier] className, [List<[DainaType]>] inheritedParentTypes, [List<[DainaObjectDeclaration]>] instanceObjects, [List<[DainaClassMethod<[DainaExpression]>]>] classMethods, [List<[DainaCompilerInjection]>] compilerInjections) {
			.selfClassName = className;
			.selfInstanceObjects = instanceObjects;
			\:output (\[String]:as "/* *** CLASS: ");
			\:output (\className:content);
			\:output (\[String]:as " *** */\n");
			\:output (\[String]:as "interface CLASSTYPE_");
			\:output (\className:content);
			\:output (\[String]:as " : OBJECT, LAMBDA");
			\inheritedParentTypes:iterate *([DainaType] parentType) {
				\:output (\[String]:as ", ");
				\parentType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] parentTypeIdentifier, [List<[DainaType]>] genericInstantiation) {
					\:output (\[String]:as "CLASSTYPE_");
					\:output (\parentTypeIdentifier:content);
				};
			};
			\:output (\[String]:as " {\n"); \:incrementTab;
			\classMethods:iterate :synthesizeClassMethodInterface;
			\:decrementTab; \:output (\[String]:as "}\n");
			\:output (\[String]:as "class CLASSIMPL_");
			\:output (\className:content);
			\:output (\[String]:as " : CLASSTYPE_");
			\:output (\className:content);
			\:output (\[String]:as " {\n"); \:incrementTab;
			\compilerInjections:iterate *([DainaCompilerInjection] compilerInjection) {
				[String] dlang = \[String]:as "dlang";
				\[O]:ifElse (\dlang:equals (\compilerInjection:identifierString)) *{
					\:output (\[String]:as "/* dlang injection start */");
					\:output (\compilerInjection:dataSegment);
					\:output (\[String]:as "/* dlang injection end */\n");
				} *{
					\:outputTabbed (\[String]:as "ERROR UNKNOWN INJECTION!!!\n");
				};
			};
			\:outputTabbed (\[String]:as "override OBJECT method(OBJECT caller, OBJECT[] parameters) { return caller; }\n");
			[Variable<[Integer]>] parentNumber = \[Variable<[Integer]>]:as (\[Integer]:one);
			\inheritedParentTypes:iterate *([DainaType] parentType) {
				[Integer] curr = \parentNumber:get;
				\:synthesizeParentObjectDeclaration curr parentType;
				\parentNumber:set (\curr:adding (\[Integer]:one));
			};
			\instanceObjects:iterate :synthesizeInstanceObjectDeclaration;
			\:iterateUnoverridenInstanceMethods class (:synthesizeParentUnoverridenMethod);
			\classMethods:iterate :synthesizeClassMethodImplementation;
			\:decrementTab; \:output (\[String]:as "}\n\n");
		};
	}

	- synthesizeInstanceObjectDeclaration *([DainaObjectDeclaration] instanceObject) {
		\:outputTabbed (\[String]:as "private OBJECT IOBJECT_");
		[DainaGeneralIdentifier] instanceObjectIdentifier = \instanceObject:identifier;
		\:output (\instanceObjectIdentifier:content);
		\:output (\[String]:as " = null;\n");
	}

	- synthesizeParentObjectDeclaration *([Integer] parentNumber, [DainaType] parentType) {
		\:outputTabbed (\[String]:as "private ");
		\parentType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] parentTypeIdentifier, [List<[DainaType]>] genericInstantiation) {
			\:output (\[String]:as "CLASSIMPL_");
			\:output (\parentTypeIdentifier:content);
		};
		\:output (\[String]:as " ");
		\:output (\[String]:as "PARENT_");
		\:output (\[String]:stringRepresentationOfInteger parentNumber);
		\:output (\[String]:as " = new ");
		\parentType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] parentTypeIdentifier, [List<[DainaType]>] genericInstantiation) {
			\:output (\[String]:as "CLASSIMPL_");
			\:output (\parentTypeIdentifier:content);
		};
		\:output (\[String]:as "();\n");
	}

	- synthesizeParentUnoverridenMethod *([Integer] parentNumber, [DainaClassMethod<[DainaExpression]>] unoverridenMethod) {
		\unoverridenMethod:isInstanceTypeOrConstructor *{
			[DainaGeneralIdentifier] unoverridenMethodIdentifier = \unoverridenMethod:identifier;
			\:outputTabbed (\[String]:as "override OBJECT IMETHOD_");
			\:output (\unoverridenMethodIdentifier:content);
			\:output (\[String]:as "(OBJECT caller, OBJECT[] parameters) { return PARENT_");
			\:output (\[String]:stringRepresentationOfInteger parentNumber);
			\:output (\[String]:as ".IMETHOD_");
			\:output (\unoverridenMethodIdentifier:content);
			\:output (\[String]:as "(caller, parameters); }\n");
		} *{} *{};
	}


	- synthesizeClassMethodInterface *([DainaClassMethod<[DainaExpression]>] classMethod) {
		\classMethod:isInstanceTypeOrConstructor *{
			[DainaGeneralIdentifier] classMethodIdentifier = \classMethod:identifier;
			\classMethod:isInstanceTypeOrConstructor *{
				\:outputTabbed (\[String]:as "OBJECT IMETHOD_");
				\:output (\classMethodIdentifier:content);
				\:output (\[String]:as "(OBJECT caller, OBJECT[] parameters);\n");
			} *{
			} *{
				@@\:outputTabbed (\[String]:as "OBJECT CMETHOD_");
				\:output (\classMethodIdentifier:content);
				\:output (\[String]:as "(OBJECT caller, OBJECT[] parameters);\n");
				\:outputTabbed (\[String]:as "OBJECT TMETHOD_");
				\:output (\classMethodIdentifier:content);
				\:output (\[String]:as "(OBJECT caller, OBJECT[] parameters);\n");@@
			};
		} *{} *{};
	}

	- synthesizeClassMethodImplementation *([DainaClassMethod<[DainaExpression]>] classMethod) {
		[DainaGeneralIdentifier] classMethodIdentifier = \classMethod:identifier;
		\[O]:ifElse (\classMethod:isUnimplementedMethod) *{
			\:outputTabbed (\[String]:as "private LAMBDA ASSIGNIMETHOD_");
			\:output (\classMethodIdentifier:content);
			\:output (\[String]:as " = null;\n");
		} *{};
		\classMethod:isInstanceTypeOrConstructor *{
			\:outputTabbed (\[String]:as "override OBJECT IMETHOD_");
		} *{
			\:outputTabbed (\[String]:as "static OBJECT TMETHOD_");
		} *{
			\:outputTabbed (\[String]:as "static OBJECT TMETHOD_");
			\:output (\classMethodIdentifier:content);
			\:output (\[String]:as "(OBJECT caller, OBJECT[] parameters) { ");
			\:output (\[String]:as "CLASSIMPL_");
			\:output (\.selfClassName:content);
			\:output (\[String]:as " newInstance = new ");
			\:output (\[String]:as "CLASSIMPL_");
			\:output (\.selfClassName:content);
			\:output (\[String]:as "(); return newInstance.CMETHOD_");
			\:output (\classMethodIdentifier:content);
			\:output (\[String]:as "(newInstance, parameters); }\n");
			\:outputTabbed (\[String]:as "OBJECT CMETHOD_");
		};
		\:output (\classMethodIdentifier:content);
		\:output (\[String]:as "(OBJECT caller, OBJECT[] parameters)");
		\:output (\[String]:as " {\n"); \:incrementTab;
		\[O]:ifElse (\classMethod:isUnimplementedMethod) *{
			\:outputTabbed (\[String]:as "return ASSIGNIMETHOD_");
			\:output (\classMethodIdentifier:content);
			\:output (\[String]:as ".method(caller, parameters);\n");
		} *{
			\:synthesizeMethodBodyContent (\classMethod:methodExpression);
		};
		\:decrementTab; \:outputTabbed (\[String]:as "}\n");
	}


	- pushScope *([List<[DainaObjectDeclaration]>] parameters) {
		[Variable<[Integer]>] paramNumber = \[Variable<[Integer]>]:as (\[Integer]:zero);
		[Integer] one = \[Integer]:one;
		\parameters:iterate *([DainaObjectDeclaration] parameter) {
			\:addLocalObjectToScope parameter;
			[DainaGeneralIdentifier] id = \parameter:identifier;
			\:outputTabbed (\[String]:as "OBJECT LOBJECT_");
			\:output (\id:content);
			\:output (\[String]:as " = parameters[");
			\:output (\[String]:stringRepresentationOfInteger (\paramNumber:get));
			\:output (\[String]:as "];\n");
			\paramNumber:set (\one:adding (\paramNumber:get));
		};
	}

	- popScope *{
		
	}

	- pushPrologueScope *{

	}

	- pushPrelogueScopeAfterPrologue *{

	}

	- popPrelogueAndPrologueScope *{

	}

	- addLocalObjectToScope *([DainaObjectDeclaration] localObject) {
		[DainaGeneralIdentifier] id = \localObject:identifier;
		\.localObjectScope:mapKeyToValue (\id:content) (\localObject:type);
	}

	- getLocalObjectType *([DainaGeneralIdentifier] localObjectIdentifier) -> [DainaType] {
		[Variable<[DainaType]>] ret = \[Variable<[DainaType]>]:as (\[DainaType]:base);
		\.localObjectScope:retriveMappedValueWithKey (\localObjectIdentifier:content) (ret:set);
	} -> \ret:get


	- synthesizeMethodBodyContent *([DainaExpression] methodExpression) {
		\[O]:selectOrDefault *([->] select) {
			\methodExpression:ifMethodExpression *([List<[DainaObjectDeclaration]>] inputs, [DainaExpression] body, [Maybe<[DainaExpression]>] output) {
				\select;
				\:pushScope inputs; 
				\:synthesizeStatement body (\[Boolean]:true);
				\output:ifNothing *{
					\:outputTabbed (\[String]:as "return caller;\n");
				};
				\output:ifIs *([DainaExpression] outputExpression) {
					\:outputTabbed (\[String]:as "return ");
					\:synthesizeInPlaceExpression (\[Boolean]:false)  outputExpression;
					\:output (\[String]:as ";\n");
				};
				\:popScope;
			};
		} *{
			\:outputTabbed (\[String]:as "return ");
			\:synthesizeInPlaceExpression (\[Boolean]:false)  methodExpression;
			\:output (\[String]:as ";\n");
		};
	}

	- synthesizeStatement *([DainaExpression] statement, [Boolean] baseStatement) {
		\[O]:selectOrDefault *([->] select) {
			\statement:ifStatementGroup *([List<[DainaExpression]>] statements) {
				\select;
				\[O]:ifElse baseStatement *{} *{
					\:pushScope (\[List<[DainaObjectDeclaration]>]:empty); 
					\:outputTabbed (\[String]:as "{\n");
					\:incrementTab;
				};
				\statements:iterate *([DainaExpression] subStatement) {
					\:synthesizeStatement subStatement (\[Boolean]:false);
				};
				\[O]:ifElse baseStatement *{} *{
					\:decrementTab;
					\:outputTabbed (\[String]:as "}\n");
					\:popScope;
				};
			};
			\statement:ifWithPrologueStatement *([DainaExpression] prelogueStatement, [DainaExpression] prologueStatement) {
				\select;
				\:pushPrologueScope;
				\:synthesizeStatement prologueStatement (\[Boolean]:true);
				\:pushPrelogueScopeAfterPrologue;
				\:synthesizeStatement prelogueStatement (baseStatement);
				\:popPrelogueAndPrologueScope;
			};
			\statement:ifLocalObjectAssignment *([DainaObjectDeclaration] declaration, [DainaExpression] assginedExpression) {
				\select;
				\:addLocalObjectToScope declaration;
				[DainaGeneralIdentifier] identifier = \declaration:identifier;
				\:outputTabbed (\[String]:as "OBJECT LOBJECT_");
				\:output (\identifier:content);
				\:output (\[String]:as " = ");
				\:synthesizeInPlaceExpression (\[Boolean]:false)  assginedExpression;
				\:output (\[String]:as ";\n");
			};
			\statement:ifInvocation *([] a, [] b) {
				\select;
				\:outputTabbed (\[String]:as "");
				\:synthesizeInPlaceExpression (\[Boolean]:false)  statement;
				\:output (\[String]:as ";\n");
			};
			\statement:ifInstanceMethodAssignment *([DainaGeneralIdentifier] identifier, [DainaExpression] assignedExpression) {
				\select;
				\:outputTabbed (\[String]:as "ASSIGNIMETHOD_");
				\:output (\identifier:content);
				\:output (\[String]:as " = ");
				\:synthesizeInPlaceExpression (\[Boolean]:false)  assignedExpression;
				\:output (\[String]:as ";\n");
			};
			\statement:ifInstanceObjectAssignment *([DainaGeneralIdentifier] identifier, [DainaExpression] assignedExpression) {
				\select;
				\:outputTabbed (\[String]:as "IOBJECT_");
				\:output (\identifier:content);
				\:output (\[String]:as " = ");
				\:synthesizeInPlaceExpression (\[Boolean]:false)  assignedExpression;
				\:output (\[String]:as ";\n");
			};
			\statement:ifConstructorInvocation *([Integer] parentNumber, [DainaGeneralIdentifier] identifier, [Boolean] isPointerConstructor, [List<[DainaExpression]>] inputs) {
				\select;
				\[O]:ifElse (\parentNumber:equals (\[Integer]:zero)) *{
					\[O]:ifElse isPointerConstructor *{
						\:output (\[String]:as "caller = ");
						\:synthesizeMethodInputs inputs;
						\:output (\[String]:as ";\n");
					} *{
						\:output (\[String]:as "this.CMETHOD_");
						\:output (\identifier:content);
						\:output (\[String]:as "(this");
						\:output (\[String]:as ", [");
						\:synthesizeMethodInputs inputs;
						\:output (\[String]:as "]);\n");
					};
				} *{
					\:outputTabbed (\[String]:as "PARENT_");
					\:output (\[String]:stringRepresentationOfInteger parentNumber);
					\[O]:ifElse isPointerConstructor *{
						\:output (\[String]:as " = ");
						\:synthesizeMethodInputs inputs;
						\:output (\[String]:as ";\n");
					} *{
						\:output (\[String]:as ".CMETHOD_");
						\:output (\identifier:content);
						\:output (\[String]:as "(PARENT_");
						\:output (\[String]:stringRepresentationOfInteger parentNumber);
						\:output (\[String]:as ", [");
						\:synthesizeMethodInputs inputs;
						\:output (\[String]:as "]);\n");
					};
				};
			};
			\statement:ifCompilerInjection *([DainaCompilerInjection] compilerInjection) {
				\select;
				[String] dlang = \[String]:as "dlang";
				\[O]:ifElse (\dlang:equals (\compilerInjection:identifierString)) *{
					\:output (\[String]:as "/* dlang injection start */");
					\:output (\compilerInjection:dataSegment);
					\:output (\[String]:as "/* dlang injection end */\n");
				} *{
					\:outputTabbed (\[String]:as "ERROR UNKNOWN INJECTION!!!\n");
				};
			};
		} *{
			\:outputTabbed (\[String]:as "INTERNAL STATEMENT ERROR!!!\n");
		};
	}

	- synthesizeMethodInputs *([List<[DainaExpression]>] inputs) {
		[Variable<[String]>] seperator = \[Variable<[String]>]:as (\[String]:as "");
		\inputs:iterate *([DainaExpression] input) {
			\:output (\seperator:get);
			\:synthesizeInPlaceExpression (\[Boolean]:false)  input;
			\seperator:set (\[String]:as ", ");
		};
	}

	- synthesizeInPlaceExpression *([Boolean] usedAsInstantMethod, [DainaExpression] expression) {
		.synthesizedExpressionType = \[DainaType]:base;
		\[O]:selectOrDefault *([->] select) {
			\expression:ifWithPrologueStatement *([DainaExpression] prelogue, [DainaExpression] prologue) {
				\select;
				\:output (\[String]:as "(new class OBJECT, LAMBDA { override OBJECT method(OBJECT caller, OBJECT[] parameters) {\n");
				\:incrementTab;
				\:synthesizeStatement prologue (\[Boolean]:true);
				\[O]:selectOrDefault *([->] selectInner) {
					\prelogue:ifStatementGroup *([] a) {
						\selectInner;
						\:synthesizeStatement prelogue (\[Boolean]:true);
						\:outputTabbed (\[String]:as "return caller;\n");
					};
				} *{
					\:outputTabbed (\[String]:as "return ");
					\:synthesizeInPlaceExpression (\[Boolean]:false)  prelogue;
					\:output (\[String]:as ";\n");
				};
				\:decrementTab;
				\:outputTabbed (\[String]:as "} }).method(caller, [])");
			};
			\expression:ifLocalObject *([DainaGeneralIdentifier] identifier) {
				\select;
				\:output (\[String]:as "LOBJECT_");
				\:output (\identifier:content);
				.synthesizedExpressionType = \:getLocalObjectType identifier;
			};
			\expression:ifMethodExpression *([List<[DainaObjectDeclaration]>] params, [DainaExpression] body, [Maybe<[DainaExpression]>] output) {
				\select;
				\:output (\[String]:as "(new class OBJECT, LAMBDA { override OBJECT method(OBJECT caller, OBJECT[] parameters) {\n");
				\:incrementTab;
				\:synthesizeMethodBodyContent expression;
				\:decrementTab;
				\:outputTabbed (\[String]:as "} })");
				[DainaType] outputType = .synthesizedExpressionType;
				.synthesizedExpressionType = \[DainaType]:base;
				\.synthesizedExpressionType:setAsMethodWithOutputType (\expression:outputType);
			};
			\expression:ifInstanceMethod *([DainaExpression] instanceExpression, [DainaGeneralIdentifier] identifier) {
				\select;
				\[O]:ifElse (usedAsInstantMethod) *{
					\:outputTabbed (\[String]:as "(cast(");
					.blockOutput = \[Boolean]:true;
					\:synthesizeInPlaceExpression (\[Boolean]:false)  instanceExpression;
					.blockOutput = \[Boolean]:false;
					\.synthesizedExpressionType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] instanceExpressionClassName, [List<[DainaType]>] insta) {
						\:output (\[String]:as "CLASSTYPE_");
						\:output (\instanceExpressionClassName:content);
					};
					\:output (\[String]:as ")");
					\:synthesizeInPlaceExpression (\[Boolean]:false)  instanceExpression;
					\:output (\[String]:as ").IMETHOD_");
					\:output (\identifier:content);
				} *{
					\:output (\[String]:as "(new class OBJECT, LAMBDA { override OBJECT method(OBJECT caller, OBJECT[] parameters) {\n");
					\:incrementTab;
					\:outputTabbed (\[String]:as "OBJECT instance = ");
					\:synthesizeInPlaceExpression (\[Boolean]:false)  instanceExpression;
					\:output (\[String]:as ";\n");
					\:outputTabbed (\[String]:as "return (cast(");
					\.synthesizedExpressionType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] instanceExpressionClassName, [List<[DainaType]>] insta) {
						\:output (\[String]:as "CLASSTYPE_");
						\:output (\instanceExpressionClassName:content);
					};
					\:output (\[String]:as ")instance).IMETHOD_");
					\:output (\identifier:content);
					\:output (\[String]:as "(instance, parameters);\n");
					\:decrementTab;
					\:outputTabbed (\[String]:as "} })");
				};
				\.synthesizedExpressionType:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] instanceExpressionClassName, [List<[DainaType]>] insta) {
					\.mappedClassMethods:retriveMappedValueWithKey (\instanceExpressionClassName:content) *([Mapping<[String][DainaClassMethod<[DainaExpression]>]>] instanceExpressionClassMethods) {
						[Maybe<[DainaClassMethod<[DainaExpression]>]>] mInvokedMethod = \instanceExpressionClassMethods:maybeRetriveMappedValueWithKey (\identifier:content);
						\mInvokedMethod:ifIs *([DainaClassMethod<[DainaExpression]>] invokedMethod) {
							[DainaExpression] methodExpression = \invokedMethod:methodExpression;
							.synthesizedExpressionType = \[DainaType]:base;
							\.synthesizedExpressionType:setAsMethodWithOutputType (\methodExpression:outputType);
						};
					};
				};
			};
			\expression:ifTypeMethod *([DainaType] type, [DainaGeneralIdentifier] identifier) {
				\select;
				\[O]:ifElse (usedAsInstantMethod) *{
					\type:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] typeIdentifier, [List<[DainaType]>] genericInstantiation) {
						\:output (\[String]:as "CLASSIMPL_");
						\:output (\typeIdentifier:content);
					};
					\:output (\[String]:as ".TMETHOD_");
					\:output (\identifier:content);
				} *{
					\:output (\[String]:as "(new class OBJECT, LAMBDA { override OBJECT method(OBJECT caller, OBJECT[] parameters) {\n");
					\:incrementTab;
					\:outputTabbed (\[String]:as "return ");
					\type:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] typeIdentifier, [List<[DainaType]>] genericInstantiation) {
						\:output (\[String]:as "CLASSIMPL_");
						\:output (\typeIdentifier:content);
					};
					\:output (\[String]:as ".TMETHOD_");
					\:output (\identifier:content);
					\:output (\[String]:as "(caller, parameters);\n");
					\:decrementTab;
					\:outputTabbed (\[String]:as "} })");
				};
				\type:ifClassNameAndGenericInstantiation *([DainaClassIdentifier] typeIdentifier, [List<[DainaType]>] genericInstantiation) {
					\.mappedClassMethods:retriveMappedValueWithKey (\typeIdentifier:content) *([Mapping<[String][DainaClassMethod<[DainaExpression]>]>] typeExpressionClassMethods) {
						[Maybe<[DainaClassMethod<[DainaExpression]>]>] mInvokedMethod = \typeExpressionClassMethods:maybeRetriveMappedValueWithKey (\identifier:content);
						\mInvokedMethod:ifIs *([DainaClassMethod<[DainaExpression]>] invokedMethod) {
							[DainaExpression] methodExpression = \invokedMethod:methodExpression;
							.synthesizedExpressionType = \[DainaType]:base;
							\.synthesizedExpressionType:setAsMethodWithOutputType (\methodExpression:outputType);
						};
					};
				};
			};
			\expression:ifInvocation *([DainaExpression] method, [List<[DainaExpression]>] params) {
				\select;
				[Variable<[Boolean]>] isInstantMethod = \[Variable<[Boolean]>]:as (\[Boolean]:false);
				\method:ifTypeMethod *([DainaType] a, [DainaGeneralIdentifier] b) {\isInstantMethod:set (\[Boolean]:true)};
				\method:ifInternalInstanceMethod *([Integer] a, [DainaGeneralIdentifier] b) {\isInstantMethod:set (\[Boolean]:true)};
				\method:ifInstanceMethod *([DainaExpression] a, [DainaGeneralIdentifier] b) {\isInstantMethod:set (\[Boolean]:true)};
				\[O]:ifElse (\isInstantMethod:get) *{
					\:synthesizeInPlaceExpression (\[Boolean]:true) method;
					\:output (\[String]:as "(caller, [");
					\:synthesizeMethodInputs params;
					\:output (\[String]:as "])");
				} *{
					\:output (\[String]:as "(cast(LAMBDA)");
					\:synthesizeInPlaceExpression (\[Boolean]:false) method;
					\:output (\[String]:as ").method(caller, [");
					\:synthesizeMethodInputs params;
					\:output (\[String]:as "])");
				};
				\.synthesizedExpressionType:ifMethodInputsAndOutput *([List<[DainaType]>] asd,[Maybe<[DainaType]>] MoutputType) {
					\MoutputType:ifIs *([DainaType] outputType) {
						.synthesizedExpressionType = outputType;
					};
				};
			};
			\expression:ifProxyObject *([DainaExpression] object) {
				\select;
				\:synthesizeInPlaceExpression (\[Boolean]:false)  object;
			};
			\expression:ifInternalInstanceMethod *([Integer] parentNumber, [DainaGeneralIdentifier] identifier) {
				\select;
				\[O]:ifElse (usedAsInstantMethod) *{
					\:output (\[String]:as "(cast(");
					\:output (\[String]:as "CLASSIMPL_");
					\:output (\.selfClassName:content);
					\:output (\[String]:as ")caller)");
					\[O]:ifElse (\parentNumber:equals (\[Integer]:zero)) *{
						\:outputTabbed (\[String]:as ".IMETHOD_");
					} *{
						\:outputTabbed (\[String]:as ".PARENT_");
						\:output (\[String]:stringRepresentationOfInteger parentNumber);
						\:output (\[String]:as ".IMETHOD_");
					};
					\:output (\identifier:content);
				} *{
					\:output (\[String]:as "(new class OBJECT, LAMBDA { override OBJECT method(OBJECT caller, OBJECT[] parameters) {\n");
					\:incrementTab;
					\:outputTabbed (\[String]:as "CLASSIMPL_");
					\:output (\.selfClassName:content);
					\:output (\[String]:as " self = ");
					\:output (\[String]:as "cast(");
					\:output (\[String]:as "CLASSIMPL_");
					\:output (\.selfClassName:content);
					\:output (\[String]:as ")caller");
					\:output (\[String]:as ";\n");
					\[O]:ifElse (\parentNumber:equals (\[Integer]:zero)) *{
						\:outputTabbed (\[String]:as "OBJECT instance = self;\n");
						\:outputTabbed (\[String]:as "return self.IMETHOD_");
					} *{
						\:outputTabbed (\[String]:as "OBJECT instance = self.PARENT_");
						\:output (\[String]:stringRepresentationOfInteger parentNumber);
						\:output (\[String]:as ";\n");
						\:outputTabbed (\[String]:as "return self.PARENT_");
						\:output (\[String]:stringRepresentationOfInteger parentNumber);
						\:output (\[String]:as ".IMETHOD_");
					};
					\:output (\identifier:content);
					\:output (\[String]:as "(instance, parameters);\n");
					\:decrementTab;
					\:outputTabbed (\[String]:as "} })");
				};
				\.mappedClassMethods:retriveMappedValueWithKey (\.selfClassName:content) *([Mapping<[String][DainaClassMethod<[DainaExpression]>]>] typeExpressionClassMethods) {
						[Maybe<[DainaClassMethod<[DainaExpression]>]>] mInvokedMethod = \typeExpressionClassMethods:maybeRetriveMappedValueWithKey (\identifier:content);
						\mInvokedMethod:ifIs *([DainaClassMethod<[DainaExpression]>] invokedMethod) {
							[DainaExpression] methodExpression = \invokedMethod:methodExpression;
							.synthesizedExpressionType = \[DainaType]:base;
							\.synthesizedExpressionType:setAsMethodWithOutputType (\methodExpression:outputType);
						};
					};
			};
			\expression:ifInternalInstanceObject *([DainaGeneralIdentifier] instanceObjectIdentifier) {
				\select;
				\:output (\[String]:as "IOBJECT_");
				\:output (\instanceObjectIdentifier:content);
				[String] instanceObjectIdentifierString = \instanceObjectIdentifier:content;
				\.selfInstanceObjects:iterate *([DainaObjectDeclaration] decl) {
					[DainaGeneralIdentifier] thisId = \decl:identifier;
					\[O]:ifElse (\instanceObjectIdentifierString:equals (\thisId:content)) *{
						.synthesizedExpressionType = \decl:type;
					} *{};
				};
			};
			\expression:ifSelfReference * {
				\select;
				\:output (\[String]:as "caller");
			};
			\expression:ifDataSegment *([String] dataSegmentContent) {
				\select;
				\:output (\[String]:as "new LITERAL_STRING(\"");
				\:output (dataSegmentContent);
				\:output (\[String]:as "\")");
			};
		} *{
			\:output (\[String]:as "<INTERNAL EXPRESSION ERROR!!!>");
		};

		@[[->]->] ifMethodSelfReference
		@[[[List<[DainaType]>][List<[DainaExpression]>][List<[DainaClassMethod<[DainaExpression]>]>]->]->] ifAnonymousClassObject
	}

	
}




